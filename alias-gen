#!/usr/bin/perl
#
# (c) 2019 Vitaly Parnas <vp330@parnas.me>

use strict;
use warnings;
use 5.010;

(($ARGV[0]) && (-f $ARGV[0])) or die "Must specify config\n";

my $OUTPUT = "$ENV{HOME}/.shortcuts";
my $DEBUG = 0;
my $section = "";
my %config = ();
my $MAX_NNN_ALIASES = 10;
my @nnn_aliases = ();

while (<>) {
    chomp;
    my ($name, $value) = ("", "");
    next if /^(#|;|\s*$)/; # comments, blanks
    s/\[(.*)\]/$section = $1/sie and next; # new config section
    unless (s/(\w+)\s*=\s*(.*)/($name, $value) = ($1, $2)/se) {
        say "Invalid line: $_";
        next;
    }
    $config{$section}{$name} = $value;
    say "$section: set $name to $value" if $DEBUG;
    if ($config{"defs"}{"BOOKMARK_NNN"} && $section eq "dirs") {
        push (@nnn_aliases, "$name:$value") if @nnn_aliases < $MAX_NNN_ALIASES;
    }
}
if ($config{"defs"}{"OUTPUT"}) {
    ($OUTPUT = $config{"defs"}{"OUTPUT"}) =~ s/(~|\$HOME)/$ENV{HOME}/;
	say "Writing aliases to $OUTPUT";
}
open(my $fs, '>', $OUTPUT) or die "Could not open file '$OUTPUT' $!";
# Write the main aliases
foreach my $section ("dirs", "files") {
	my $template = $config{"cmd_templates"}{$section};
	foreach my $name (keys %{$config{$section}}) {
		my $value = $config{$section}{$name};
		printf($fs "alias $name=\"$template\"\n", $value);
	}
}
if ($config{"defs"}{"BOOKMARK_NNN"}) {
    say "Setting NNN aliases" if $DEBUG;
    print($fs "export NNN_BMS=\"");
    for my $i (0..(@nnn_aliases - 1)) {
        print($fs ";") if ($i > 0);
        print($fs $nnn_aliases[$i]); 
    }
    print($fs "\"\n");
}
close $fs;
